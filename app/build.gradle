apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.github.kt3k.coveralls'
apply from: '../app/coverage.gradle'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    defaultConfig {
        applicationId "apidez.com.doit"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    signingConfigs {
        release {
            storeFile file("keystore.jks")
            storePassword "developer"
            keyAlias "nongdenchet"
            keyPassword "developer"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            testCoverageEnabled true
        }
    }

    productFlavors {
        prod {
            applicationIdSuffix = ".prod"
            resValue "string", "app_name", "Do.it"
        }
        mock {
            applicationIdSuffix = ".mock"
            resValue "string", "app_name", "Do.it Mock"
        }
    }

    android.variantFilter { variant ->
        if (variant.buildType.name.equals('release') && variant.getFlavors().get(0).name.equals('mock')) {
            variant.setIgnore(true);
        }
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    sourceSets {
        prod.java.srcDirs = ['src/main/java', 'src/data/java']
    }
}

configurations {
    androidTestCompile.exclude group: 'com.android.support', module: 'support-v4'
    androidTestCompile.exclude group: 'com.android.support', module: 'recyclerview-v7'
    androidTestCompile.exclude group: 'com.android.support', module: 'appcompat-v7'
    androidTestCompile.exclude group: 'com.android.support', module: 'support-annotations'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Support library
    compile "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:design:$rootProject.supportLibraryVersion"
    compile "com.android.support:recyclerview-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:cardview-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support.test.espresso:espresso-idling-resource:$rootProject.espressoVersion"

    // Dagger
    compile "org.glassfish:javax.annotation:$rootProject.annotaionVersion"
    compile "com.google.dagger:dagger:$rootProject.daggerVersion"
    apt "com.google.dagger:dagger-compiler:$rootProject.daggerVersion"

    // Others
    compile "com.jakewharton:butterknife:$rootProject.butterknifeVersion"
    compile "de.greenrobot:eventbus:$rootProject.eventbusVersion"

    // UI
    compile "de.hdodenhof:circleimageview:$rootProject.circleImageViewVersion"
    compile "com.wdullaer:materialdatetimepicker:$rootProject.datePickerVersion"

    // RxAndroid
    compile "io.reactivex:rxandroid:$rootProject.rxandroidVersion"

    // Database ORM
    compile "com.github.satyan:sugar:$rootProject.sugarVersion"

    // Unit Test
    testCompile "org.assertj:assertj-core:$rootProject.assertjVersion"
    testCompile "junit:junit:$rootProject.junitVersion"
    testCompile "org.hamcrest:hamcrest-core:$rootProject.hamcrestVersion"
    testCompile "org.mockito:mockito-core:$rootProject.mockitoVersion"
    testCompile "org.robolectric:robolectric:$rootProject.robolectricVersion"

    // Android Test
    androidTestCompile "org.mockito:mockito-core:$rootProject.mockitoVersion"
    androidTestCompile "com.android.support.test:runner:$rootProject.runnerVersion"
    androidTestCompile "com.android.support.test:rules:$rootProject.rulesVersion"
    androidTestCompile "com.android.support:support-annotations:$rootProject.supportLibraryVersion"
    androidTestCompile "com.android.support.test.espresso:espresso-core:$rootProject.espressoVersion"
    androidTestCompile "com.android.support.test.espresso:espresso-contrib:$rootProject.espressoVersion"
}
